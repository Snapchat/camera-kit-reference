apply plugin: 'com.android.application'

android {
    def cameraKitDynamicPluginApplicationId = 'com.snap.camerakit.sample.dynamic.plugin'

    compileSdkVersion androidCompileSdkVersion

    defaultConfig {
        applicationId 'com.snap.camerakit.sample.dynamic.app'

        minSdkVersion androidMinSdkVersion
        targetSdkVersion androidTargetSdkVersion
        versionCode cameraKitDistributionCode
        versionName cameraKitDistributionVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        manifestPlaceholders = [
            'cameraKitApiToken': cameraKitApiToken,
            'cameraKitDynamicPluginApplicationId': cameraKitDynamicPluginApplicationId
        ]

        buildConfigField 'String', "LENS_GROUP_ID_TEST", "\"$cameraKitLensesGroupId\""
        buildConfigField 'String', "DYNAMIC_PLUGIN_CAMERAKIT", "\"$cameraKitDynamicPluginApplicationId\""
    }

    buildTypes {
        debug {}
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        debug {
            storeFile rootProject.file('debug.keystore')
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }
}

dependencies {
    implementation "androidx.appcompat:appcompat:$appCompatVersion"
    implementation "androidx.recyclerview:recyclerview:$recyclerViewVersion"
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    api "com.snap.camerakit:camerakit-plugin-api:$cameraKitVersion"

    // When CameraKit SDK is loaded from another apk, the CameraKit API classes MUST not be included
    // in the host application, they can only be compiled against to avoid loading out-of-date
    // version of the SDK API at runtime before CameraKit Plugin.Loader has a chance to load latest
    // classes.
    compileOnly "com.snap.camerakit:camerakit-api:$cameraKitVersion"
}
